def BUILD_NUMBER = project.hasProperty('BUILD_NUMBER') ? "$BUILD_NUMBER" : 'undef'

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    project.group = 'com.tzachz'
    project.version = "0.1.$BUILD_NUMBER"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        beforeTest { descriptor ->
            logger.lifecycle("Running " + descriptor)
        }
    }
}

subprojects { project ->
    dependencies {
        compile 'com.google.guava:guava:14.0.1'
        compile 'joda-time:joda-time:2.2'
        compile 'org.slf4j:slf4j-api:1.7.5'

        testCompile 'org.hamcrest:hamcrest-library:1.3'
        testCompile 'org.mockito:mockito-core:1.9.0'
        testCompile('junit:junit:4.11') {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

idea {
    project {
        jdkName = '1.7'
    }
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
    }
}

